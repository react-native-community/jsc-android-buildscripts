apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

def distDir = project.findProperty("distDir") ?: ""
def jniLibsDir = project.findProperty("jniLibsDir") ?: ""
def revision = project.findProperty("revision") ?: "".replaceAll("\\s", "")

if (!distDir) throw new RuntimeException("expecting --project-prop distDir=??? but was empty")
if (!jniLibsDir) throw new RuntimeException("expecting --project-prop jniLibsDir=??? but was empty")
if (!revision) throw new RuntimeException("expecting --project-prop revision=??? but was empty")

android {
  namespace 'org.webkit.androidjsc_cppruntime'
  compileSdkVersion 35

  defaultConfig {
    minSdkVersion 24
    targetSdkVersion 34
    versionCode 1
    versionName "1.0"
  }

  sourceSets {
    main {
      jniLibs.srcDirs = ["${jniLibsDir}"]
    }
  }

  publishing {
    singleVariant("release") {
    }
  }
}

dependencies {}

project.group = "org.webkit"
project.version = "r${revision}"

afterEvaluate {
  publishing {
    publications {
      release(MavenPublication) {
        from components.release
        pom {
          name = "android-jsc"
          artifactId = "android-jsc-cppruntime"
          packaging = "aar"
        }
      }
    }
    repositories {
      maven {
        url = "file://${distDir}"
      }
    }
  }
}

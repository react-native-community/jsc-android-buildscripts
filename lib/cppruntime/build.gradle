apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

def distDir = project.findProperty("distDir") ?: ""
def jniLibsDir = project.findProperty("jniLibsDir") ?: ""
def version = project.findProperty("version") ?: "".replaceAll("\\s", "")

if (!distDir) throw new RuntimeException("expecting --project-prop distDir=??? but was empty")
if (!jniLibsDir) throw new RuntimeException("expecting --project-prop jniLibsDir=??? but was empty")
if (!version) throw new RuntimeException("expecting --project-prop version=??? but was empty")

android {
  namespace 'io.github.react_native_community.jscandroid_cppruntime'
  compileSdkVersion 35

  defaultConfig {
    minSdkVersion 24
    targetSdkVersion 34
    versionCode 1
    versionName "1.0"
  }

  sourceSets {
    main {
      jniLibs.srcDirs = ["${jniLibsDir}"]
    }
  }

  publishing {
    singleVariant("release") {
    }
  }
}

dependencies {}

project.group = "io.github.react-native-community"
project.version = "${version}"

afterEvaluate {
  publishing {
    publications {
      release(MavenPublication) {
        from components.release
        pom {
          name = "jsc-android"
          artifactId = "jsc-android-cppruntime"
          packaging = "aar"
        }
      }
    }
    repositories {
      maven {
        url = "file://${distDir}"
      }
    }
  }
}

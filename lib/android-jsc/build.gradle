apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

def distDir = project.findProperty("distDir") ?: ""
def jniLibsDir = project.findProperty("jniLibsDir") ?: ""
def revision = project.findProperty("revision") ?: "".replaceAll("\\s", "")
def i18n = project.findProperty("i18n") ?: ""

if (!distDir) throw new RuntimeException("expecting --project-prop distDir=??? but was empty")
if (!jniLibsDir) throw new RuntimeException("expecting --project-prop jniLibsDir=??? but was empty")
if (!revision) throw new RuntimeException("expecting --project-prop revision=??? but was empty")
if (!i18n) throw new RuntimeException("expecting --project-prop i18n=??? but was empty")

android {
  namespace 'org.webkit.androidjsc'
  compileSdkVersion 35

  defaultConfig {
    minSdkVersion 24
      targetSdkVersion 34
      versionCode 1
      versionName "1.0"
  }

  sourceSets {
    main {
      jniLibs.srcDirs = ["${jniLibsDir}"]
    }
  }

  packagingOptions {
    doNotStrip "**/libjsc.so"
  }

  publishing {
    singleVariant("release") {
    }
  }
}

dependencies {}

project.group = "org.webkit"
def artifactName = Boolean.valueOf(i18n) ? "android-jsc-intl" : "android-jsc"
project.version = "r${revision}"

afterEvaluate {
  publishing {
    publications {
      release(MavenPublication) {
        from components.release
        pom {
          name = "android-jsc"
          artifactId = artifactName
          packaging = "aar"
        }
      }
    }
    repositories {
      maven {
        url = "file://${distDir}"
      }
    }
  }
}

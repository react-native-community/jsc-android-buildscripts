// Top-level build file where you can add configuration options common to all sub-projects/modules.

ext {
    JSC_DIR = "$rootDir/../../dist"
    def jscAAR = fileTree(dir: JSC_DIR).find { it.name.endsWith(".aar") }
    JSC_VERSION = jscAAR ? file(file(jscAAR).getParent()).getName() : ""
    println JSC_VERSION

    def i18nProp = true //project.findProperty("i18n")
    JSC_NAME = Boolean.valueOf(i18nProp) ? "android-jsc-intl" : "android-jsc"
}

allprojects {
    configurations.all {
        resolutionStrategy {
            eachDependency { DependencyResolveDetails details ->
                if (details.requested.name == 'android-jsc') {
                    details.useTarget group: details.requested.group, name: JSC_NAME, version: JSC_VERSION
                }
            }
        }
    }
}

buildscript {
    ext {
        buildToolsVersion = "27.0.3"
        minSdkVersion = 21
        compileSdkVersion = 27
        targetSdkVersion = 26
        supportLibVersion = "27.1.1"
    }
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.4'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        maven {
            url JSC_DIR
        }
        mavenLocal()
        google()
        jcenter()
        maven {
            // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
            url "$rootDir/../node_modules/react-native/android"
        }
    }
}


task wrapper(type: Wrapper) {
    gradleVersion = '4.4'
    distributionUrl = distributionUrl.replace("bin", "all")
}
